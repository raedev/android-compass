plugins {
    id 'com.android.library'
    id 'maven-publish'
}


android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.github.raedev.compass'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
}

def libVersion = '1.0.2'
if (new File(project.projectDir, "upload.gradle").exists()) {
    ext {
        ARTIFACT_URL = 'https://github.com/raedev/android-compass'
        ARTIFACT_GROUP = 'com.github.raedev'
        ARTIFACT_ID = 'compass'
        ARTIFACT_VERSION = libVersion
    }
    apply from: 'upload.gradle'
} else {
    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                    groupId = 'com.github.raedev'
                    artifactId = 'compass'
                    version = libVersion
                    pom {
                        name = 'android-compass'
                        url = 'https://github.com/raedev/android-compass'
                        developers {
                            developer {
                                id = 'RAE'
                                name = 'RAE'
                                email = 'raedev@qq.com'
                            }
                        }
                    }
                }
            }
        }
    }
}